buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

allprojects {
    group 'net.kunmc.lab'
    version '0.8.2'
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "idea"
    apply plugin: "com.github.johnrengelman.shadow"

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version

                from components.java
            }
        }
    }

    jar {
        doFirst {
            copy {
                from project.rootDir.toPath().toAbsolutePath()
                into buildDir.name + '/resources/main/'
                include 'LICENSE*'
            }
        }
    }

    shadowJar {
        archiveFileName = "${project.name}-${project.version}.jar"
    }
    tasks.build.dependsOn tasks.shadowJar
}
